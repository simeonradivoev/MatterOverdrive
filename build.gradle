// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply from: 'gradle/scripts/dependencies.gradle'
apply from: 'gradle/scripts/artifacts.gradle'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "ForgeFS"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "${config.mc_version}-${config.mod_version}"

if (System.getenv('TRAVIS_BUILD_NUMBER') != null) {
    version += "-build-" + System.getenv('TRAVIS_BUILD_NUMBER')
}

group= "${config.mo_group}"
archivesBaseName = "${config.mo_base_name}"

minecraft {
    version = config.mc_version + "-" + config.forge_version
    runDir = "run"
    mappings = config.mcp_mappings

    makeObfSourceJar = false

    replaceIn "Reference.java"
    replace "@VERSION@", config.mod_version
}

processResources {
    inputs.property "version", config.mod_version
    inputs.property "mcversion", config.mc_version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": config.mod_version, "mcversion": config.mc_version
    }
    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

bintray {
    user = 'simeonradivoev'
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'MatterOverdrive'
        name = 'mo'
        userOrg = 'simeonradivoev'
        version {
            name = config.mc_version + '-' + config.mod_version
            released  = new Date()
            desc = System.getenv('TRAVIS_COMMIT')
        }
        filesSpec {
            from 'build/libs'
            into 'builds'
        }
        publish = true
    }
}
